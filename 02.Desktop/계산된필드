<범례>
  가) ## : 태블로 계산된 필드 이름 
  나) // : 주석, 설명
  나) ==============끝================ : 계산식 끝 (개별 계산식 구분)
  라) -------------------------------- : 하위구분선 (유사 계산식 구분)

<이하부터 계산식>
## 2023-2024수주년월
// 결재승인일자 문자열(str) 필드로부터 수주년도(date) 인식한다.

  DATE(IF YEAR(DATEPARSE("yyyyMM", [내부승인년월 (2023수주내역)])) = 2023
     OR YEAR(DATEPARSE("yyyyMM", [내부승인년월 (2023수주내역)])) = 2024
  THEN DATEPARSE("yyyyMM", [내부승인년월 (2023수주내역)])
  ELSEIF YEAR(DATEPARSE("yyyyMM", [내부승인년월])) = 2023 
     OR YEAR(DATEPARSE("yyyyMM", [내부승인년월])) = 2024
  THEN DATEPARSE("yyyyMM", [내부승인년월])
  END)

==============끝================

## 2023-2024수주년월(기준월TF)
// 년도 기준 T/F 판별한다.

  IF DATEPART('month', [2023-2024수주년월]) <= DATEPART('month', [오늘날짜]) THEN
      TRUE
  ELSE
      FALSE
  END

==============끝================

## 2023수주년도 & 2024수주년도 (년도별 데이터 원본 상이)
// 문자열(str)필드로 당해년도를 구분한다.

  DATE(DATEPARSE ( "yyyy", LEFT([내부승인년월 (2023수주내역)], 4) ))
  ----------------------------
  DATE(DATEPARSE ( "yyyy", LEFT([내부승인년월], 4) ))

==============끝================

## 2023수주년월 & 2024수주년월 (년도별 데이터 원본 상이)
// 문자열(str) 필드로부터 년월(Date) 인식한다.

  DATE(DATEPARSE("yyyyMM", [내부승인년월 (2023수주내역)]))
  ----------------------------
  DATE(DATEPARSE("yyyyMM", [내부승인년월]))

==============끝================

## 2024담당부서 & 2024담당자
// 당해년도에 해당하는 담당부서, 담당자 필드만 추출한다. (관리번호 PK기준으로 조인과정에서 생성된 중복값 제거)

  { FIXED [관리번호] : 
      MIN(
          IF LEFT([내부승인년월], 4) = "2024" THEN [담당부서]
          END
      )
  }
  ----------------------------
  { FIXED [관리번호] : 
      MIN(
          IF LEFT([내부승인년월], 4) = "2024" THEN [담당자]
          END
      )
  }

==============끝================

## 2024사업분류
// 당해년도에 해당하는 사업분류 필드를 생성한다.

  { FIXED [관리번호] : 
      MIN(
          IF LEFT([내부승인년월], 4) = "2024" THEN [사업분류]
          END
      )
  }

==============끝================

## Top N 적용필터(담당)
// 금액(int) 매개변수 및 집합생성 후 담당자별 Top N을 적용하는 필터를 생성한다.

  IF [Top N 집합(담당)] THEN
      "01.Top N 이내"
  ELSEIF NOT [Top N 집합(담당)] AND YEAR([년월]) <> 2023 THEN
      "02.Top N 이외"
  END
  ----------------------------
  IF [Top N 집합(담당)] THEN
      "01.Top N 이내"
  ELSEIF NOT [Top N 집합(담당)] AND YEAR([년월]) <> 2023 THEN
      "02.Top N 이외"
  ELSE
      "Null"
  END

==============끝================

## Top N 적용필터(수주목록)
// 금액(int) 매개변수 및 집합생성 후 사업별 Top N을 적용하는 필터를 생성한다.

  IF [Top N 집합(수주목록)] THEN "01.Top N 이내" ELSE "02.Top N 이외" END

==============끝================

## 계약서제출여부
// 상태 필드의 값에 따라 계약서 제출여부를 판단하는 필터를 생성한다.

  IF [상태] = '계약완료' THEN '최종계약서 미제출' ELSE '정상' END

==============끝=================

## 공급가평균선택
//기준단가 충족 여부 및 미달 건을 구분하기 위한 필터를 생성한다.

  IF [2024납품단가(Vat별도)] >= { FIXED: AVG([2024납품단가(Vat별도)]) } THEN '01.제품공급가 평균이상'
  ELSE '02.제품공급가 평균미만'
  END

==============끝================

## 년월(기준월TF)
// 매출기준 당해년도 설정한 뒤 월별 매출을 추정한다.

  IF DATEPART('year', [매출기준월]) = 2024 THEN
      IF DATEPART('month', [년월]) <= DATEPART('month', [매출기준월]) THEN
          TRUE
      ELSE
          FALSE
      END
  ELSE
      FALSE
  END

==============끝================

## 2023유지보수수주액 (별도 데이터 원본 소스)
// 조인으로 인한 중복값을 각월기준으로 제거하고 전년도 수주금액 총액산출한다.

  { FIXED [수주월 (2023수주내역)] : MIN([수주액 (2023수주내역)]) }

==============끝================

## 2024납품단가(Vat별도)
// 조인으로 인한 중복값을 각월기준으로 제거하고 당해년도 수주금액 총액산출한다.

  { FIXED [관리번호] : 
      MAX(
          IF INT(LEFT([내부승인년월], 4)) >= 2024 THEN [원계약납품가액(Vat별도)]
          ELSE NULL
          END
      )
  }

==============끝================

## 2024매출
// 당해년도 매출액 계산

  IF YEAR([년월]) = 2024 THEN [년월 별 매출] END

==============끝================

## 2024요율(%/예외값보정)
// 관리번호 기준으로 중복값을 제거하고, 예외값을 제거한 당해년도 요율을 계산한다.

  { FIXED [관리번호] : 
      MIN(
          IF LEFT([내부승인년월], 4) >= "2024" 
             AND NOT ISNULL([내부승인년월]) 
             AND [내부승인년월] != "NaNNaN" 
             AND [요율(%)] >= 5 
             AND [요율(%)] <= 25
          THEN [요율(%)] 
          ELSE NULL 
          END
      ) 
  }

==============끝================

## 2024유지보수수주액
// 관리번호 기준으로 중복값 제거, 당해년도 해당하는 수주액만 필터링 합산한다.

  { FIXED [관리번호] : 
      MAX(
          IF INT(LEFT([내부승인년월], 4)) >= 2024 THEN [유지보수 수주액(Vat별도)]
          ELSE NULL
          END
      )
  }

==============끝================

## 유지보수평균월매출(수주기반)
//중복값 제거한 당해년도 매출을 그 달의 일수로 나누어 합산, 해당하는 데이터 건수로 나누어 각 월평균 계산한다.

  { FIXED [관리번호] : 
    SUM(
      IF INT(LEFT([내부승인년월], 4)) >= 2024 THEN
        [년월 별 매출] / 
        DAY(DATE(DATEPARSE("yyyyMM", [내부승인년월])))
      ELSE 
        0
      END
    ) / 
    NULLIF(
      COUNT(
        IF INT(LEFT([내부승인년월], 4)) >= 2024 THEN
          [내부승인년월]
        ELSE 
          NULL
        END
      ), 
      0
    )
  }

==============끝================

## 2024전체요율평균
// 전체 데이터 집합에 대해 특정 차원이나 필터적용되지 않는 당해년도 전체요율평균을 계산한다.
  { FIXED : AVG([2024요율(%/예외값보정)]) }

==============끝================

## 오늘날짜(YYYYMM)
// 오늘날짜(date)를 문자열(str) YYYYMM으로 변환한다

  DATE(DATEPARSE ( "yyyyMM", // 오늘 날짜를 YYYYMM 형식으로 변환
  STR(YEAR([오늘날짜])) + RIGHT('0' + STR(MONTH([오늘날짜])), 2) ))

==============끝================

## 전체잔여건
// 계산된 잔여계약 T/F 기반 전체 데이터 집합에 대해 특정 차원이나 필터적용되지 않는 당해년도 잔여 건을 계산한다.

  { FIXED : COUNT(IF [2024잔여계약TF] THEN [유지보수계약명 (상세내역)] END) }

==============끝================

## 2024 매출 Top N
// 생성한 TopN 매개변수와 년월 기준으로 필터링 하여 당해년도 매출액 순 Top N을 필터한다.

  IF RANK(SUM(IF YEAR([년월]) = 2024 THEN [년월 별 매출] END)) <= [Top N(수주/매출/잔여목록)] THEN "01.Top N 이내"
  ELSE "02.Top N 이외"
  END

==============끝================

## 2024목표달성율
// 당해년도 승인된 유지보수 수주액이 전체 수주액(2024년도 수주액 + 잔여 계약 수주액)에서 차지하는 비율을 계산한다.

  IF SUM(
    IF [내부승인년월 (상세내역)] >= 202401
       AND [내부승인년월 (상세내역)] <= 202412
       THEN [유지보수 수주액(Vat별도) (상세내역)]
       ELSE 0
    END
  ) + 
  SUM(
    IF [2024잔여계약TF]
       THEN [유지보수 수주액(Vat별도) (상세내역)]
       ELSE 0
    END
  ) != 0 THEN
    (SUM(
      IF [내부승인년월 (상세내역)] >= 202401
         AND [내부승인년월 (상세내역)] <= 202412
         THEN [유지보수 수주액(Vat별도) (상세내역)]
         ELSE 0
      END
    ) / 
    (
      SUM(
        IF [내부승인년월 (상세내역)] >= 202401
           AND [내부승인년월 (상세내역)] <= 202412
           THEN [유지보수 수주액(Vat별도) (상세내역)]
           ELSE 0
        END
      ) + 
      SUM(
        IF [2024잔여계약TF]
           THEN [유지보수 수주액(Vat별도) (상세내역)]
           ELSE 0
        END
      )
    )) * 100
  ELSE
    0
  END

==============끝================

## 2024수주건 & 2024수주액
// 전체 데이터 집합에 대해 특정 차원이나 필터적용되지 않는 당해년도 수주건/수주액 고정값을 계산한다. 

  { FIXED : COUNT(
    IF [내부승인년월 (상세내역)] >= 202401 AND [내부승인년월 (상세내역)] <= 202412
    THEN [유지보수 수주액(Vat별도) (상세내역)]
    END
  ) }

  ----------------------------

  { FIXED : SUM(
    IF [내부승인년월 (상세내역)] >= 202401 AND [내부승인년월 (상세내역)] <= 202412 
    THEN [유지보수 수주액(Vat별도) (상세내역)] 
    ELSE 0 
    END
  ) }

==============끝================

## 2024유지보수평균월매출
// 당해년도 월별 매출 합계를 구한 뒤 평균내어 당해년도 평균월매출 계산한다.

  AVG(
    { INCLUDE [년월] : SUM(
      IF YEAR([년월]) = 2024 THEN [년월 별 매출] END
    )}
  )

==============끝================

## 2024잔여계약요율(%/예외값보정)
//계약되지 않은 잔여 계약 건들에 대한 요율 평균계산(예외값 제거)

  AVG(
      IF [2024잔여계약TF] AND [요율(%) (상세내역)] >= 5 AND [요율(%) (상세내역)] < 25 THEN 
          [요율(%) (상세내역)] 
      END
  )

==============끝================

## 관리대상 구분
// 납품금액 * 요율 상관관계 차트에서 각 사분면에 해당하는 건을 식별하는 필터 계산식

  IF SUM([2024납품단가(Vat별도)]) > WINDOW_AVG(SUM([2024납품단가(Vat별도)]))
    AND WINDOW_AVG(SUM([2024요율(%/예외값보정)])) <= 9
  THEN '1사분면(중점관리대상)'

  ELSEIF SUM([2024납품단가(Vat별도)]) > WINDOW_AVG(SUM([2024납품단가(Vat별도)]))
    AND WINDOW_AVG(SUM([2024요율(%/예외값보정)])) > 11
  THEN '2사분면(계약구조양호)'

  ELSEIF SUM([2024납품단가(Vat별도)]) <= WINDOW_AVG(SUM([2024납품단가(Vat별도)]))
    AND WINDOW_AVG(SUM([2024요율(%/예외값보정)])) <= 9
  THEN '3사분면(개선효과낮음)'

  ELSE '4사분면(개선여지없음)'
  END

==============끝================

## 예년대비수주증가율(%)
// 전년수주액 대비 올해수주액의 증가 비율을 계산한다.

  ((SUM([2024유지보수수주액]) - SUM([2023유지보수수주액])) / SUM([2023유지보수수주액])) * 100

==============끝================

## 예년대비수주차액(기호) & 전년대비매출차액(기호)
// 예년동월차액 조건에 따라 상승 or 하강 기호가 동적으로 정해진다.

  IF RUNNING_SUM([전년동월차액]) > 0 THEN '▲' 
    ELSEIF RUNNING_SUM([전년동월차액]) < 0 THEN '▼' 
    ELSE '' 
    END

  ----------------------------

// 전년매출차액 조건에 따라 상승 or 하강 기호가 동적으로 정해진다.
  IF RUNNING_SUM([전년대비매출차액]) > 0 THEN '▲' 
  ELSEIF RUNNING_SUM([전년대비매출차액]) < 0 THEN '▼' 
  ELSE '' 
  END

==============끝================

## 전년대비매출성장율(%) & 전년대비매출차액
// 전월 매출합계를 가져와 현재 월 매출의 전월대비 증감을 계산한다.
  ((SUM([년월 별 매출]) - LOOKUP(SUM([년월 별 매출]), -1)) / LOOKUP(SUM([년월 별 매출]), -1)) * 100

  ----------------------------

// 전월 매출합계를 가져와 현재 월 매출의 전월대비 차액을 계산한다.
  SUM([년월 별 매출]) - LOOKUP(SUM([년월 별 매출]), -1)

==============끝================

## 전년동월차액(기호)
// 전년동월차액 조건에 따라 상승 or 하강 기호가 동적으로 정해진다.

  IF [전년동월차액] > 0 THEN '▲' 
  ELSEIF [전년동월차액] < 0 THEN '▼' 
  ELSE '' 
  END

==============끝================
